plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'idea'
}

version = '3.0.0-SNAPSHOT'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "io.vertx:vertx-unit:$vertxVersion"
}

test {
    systemProperty 'vertx.logger-delegate-factory-class-name', "io.vertx.core.logging.impl.SLF4JLogDelegateFactory"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'com.deblox.Boot'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}